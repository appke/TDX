
15,2

Source Code:
{----------VE 口袋支点-----------}
X:=EXTDATA_USER(1,0);
RPS120:=X/10;
Y:=EXTDATA_USER(2,0);
RPS250:=Y/10;
Z:=EXTDATA_USER(3,0);
RPS50:=Z/10;

KD1:=RPS250>=90 OR RPS120>=90 OR RPS50>=90;

FKD21:=AMO=HHV(AMO,10);{创10日的最高成交金额}
FKD22:=C/REF(C,1)>1.099;{当日上涨超过9.9%}
FKD23:=AMO/MA(AMO,10)>2;{成交金额是10天平均的一倍以上}
KD2:=FKD21 OR FKD22 OR FKD23; {创10日的最高成交金额，或者当日上涨超过9.9%，或者成交金额是10天平均的一倍以上}

FKD311:=C>MA(C,90);
FKD312:=MA(C,90)>=REF(MA(C,90),5);
FKD313:=H>=HHV(H,90);
FKD31:=FKD311 AND FKD312 AND FKD313;
FKD321:=C>MA(C,100);
FKD322:=MA(C,100)>=REF(MA(C,100),5);
FKD323:=H>=HHV(H,100);
FKD32:=FKD321 AND FKD322 AND FKD323 AND FKD312;
FKD331:=C>MA(C,120);
FKD332:=MA(C,120)>=REF(MA(C,120),2);
FKD33:=FKD331 AND FKD332;
KD3:=FKD31 OR FKD32 OR FKD33;

FKD250:=H>=HHV(H,250);
KD4:=LLV(L,15)>LLV(L,50)*0.995 OR FKD250;

H120:=HHV(H,120);
T1:=HHVBARS(H,120);
L120:=LLV(L,T1+1);
FKD51:=LLV(L,40)/HHV(H,120)>0.5;
FKD5:=FKD51 OR FKD250;
KD5:=L120/H120>0.54 AND FKD5;

FKD41:=FKD250 OR LLV(L,15)>LLV(L,50);
{15日内的最低价不是50日内的最低价，或者创250日的最高价}
FKD42:=LLV(L,15)=LLV(L,50) AND LLV(L,15)>LLV(L,100) AND H/HHV(H,250)>0.88;
{15日内的最低价等于50日内的最低价，但不是100天内的最低价，且接近一年新高}
FKD43:=LLV(L,15)=LLV(L,50) AND LLV(L,15)>LLV(L,100) AND H/HHV(H,250)>0.75 AND H>=HHV(H,40) AND C/REF(C,1)>1.07;
{15日内的最低价等于50日内的最低价，但不是100天内的最低价，且接近一年新高，且创40日的最高价，且当天涨幅大于7%}
KD6:=FKD41 OR FKD42 OR FKD43;{结构紧凑的重要条件}
KD7:=C/REF(C,1)>=1.05;

KDZD:=KD1 AND KD2 AND KD3 AND KD4 AND KD5 AND KD6 AND KD7 ;
KDZDXG:=BARSSINCEN(KDZD,15)=0;
DRAWICON(KDZDXG,LOW*0.993,38);{口袋支点信号画图标38:红上三角};
