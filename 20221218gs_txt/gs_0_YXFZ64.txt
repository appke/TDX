月线反转6.4
15,2

Source Code:
Z50:=EXTDATA_USER(3,0);{50天的}
RPS50:=Z50/10;
FYX11:=IF(RPS50>=87,1,0);{RPS50大于87}


Z120:=EXTDATA_USER(1,0);{120天的}
RPS120:=Z120/10;
FYX12:=IF(RPS120>=90,1,0);{RPS120大于90}


FYX130:=RPS50>=90 OR RPS120>=90;{当天RPS50或RPS120大于90，在后面被FYX32引用}
FYX131:=C>=HHV(C,70);{创70日最高收盘价}
FYX13:=FYX130 AND FYX131;{创70日新高，且当天RPS50或RPS120大于90。在后面被FYX21、FYX22、FYX63、FYX72等引用}


FYX1:=FYX11 OR FYX12;


FYX21:=LLV(L,50)>LLV(L,200) AND FYX13;{50日内最低价大于200日内最低价}
FYX22:=LLV(L,30)>LLV(L,120) AND FYX13;{30日内最低价大于120日内最低价，且FYX13}
FYX23:=LLV(L,20)>LLV(L,50);{20日内最低价大于50日内最低价，顺鑫农业2018年4月2日的月线反转信号}
FYX2:=FYX21 OR FYX22 OR FYX23;{结构紧凑的重要条件}


NH80:=IF(H<HHV(H,80),0,1);
FYX31:=COUNT(NH80,10);{10天内曾创80日新高}
FYX32:=(C>=HHV(C,50) OR H>=HHV(H,50)) AND FYX130;{当天创50日最高收盘价或50日最高价，且RPS50或RPS120大于90}
FYX3:=FYX31 OR FYX32;


FYX4:=C>MA(C,20) AND C>MA(C,200) AND MA(C,120)/MA(C,200)>0.9;{当天收盘价必须站上20天线和200天线}


NN200:=IF(C>MA(C,200),1,0);{当天收盘价大于200天线}
AA200:=COUNT(NN200,45);
NN250:=IF(C>MA(C,250),1,0);{当天收盘价大于250天线}
AA250:=COUNT(NN250,45);
FYX51:=AA200>=2 AND AA200<45;{45天内，收盘价站上200天线的天数大于等于2，小于45}
LNN200:=IF(L<MA(C,200),1,0);
LAA200:=COUNT(LNN200,45);
FYX52:=LAA200>0 AND AA200>2;{45天内，至少有一天的最低价低于200天线;且至少站上200天线3天以上}
LNN250:=IF(L<MA(C,250),1,0);
LAA250:=COUNT(LNN250,45);
FYX53:=LAA250>0 AND AA250>2;{45天内，至少有一天的最低价低于250天线;且至少站上250天线3天以上}
FYX5:=FYX51 OR FYX52 OR FYX53;


FYX6011:=MA(C,120)>=REF(MA(C,120),10) OR MA(C,200)>=REF(MA(C,200),10);{120天线或200天线呈上升趋势}
FYX6012:=MA(C,120)>=REF(MA(C,120),15) OR MA(C,200)>=REF(MA(C,200),15);{120天线或200天线呈上升趋势}
FYX601:=FYX6011 OR FYX6012;
FYX6021:=MA(C,120)>=REF(MA(C,120),10) AND MA(C,200)>=REF(MA(C,200),10);{120天线和200天线线呈上升趋势}
FYX6022:=MA(C,120)>=REF(MA(C,120),15) AND MA(C,200)>=REF(MA(C,200),15);{120天线和200天线线呈上升趋势}
FYX602:=FYX6021 OR FYX6022;
FYX603:=MA(C,120)>MA(C,200) AND FYX601; {120日线、200日线呈多头排列}
FYX61:=HHV(H,30)/LLV(L,120)<1.50 AND FYX601;{30天内最高价与120日内最低价之比小于1.50，且120天线或200天线呈上升趋势，石英股份2022年的平台在120天左右}
FYX62:=HHV(H,30)/LLV(L,120)<1.55 AND FYX602;{30天内最高价与120日内最低价之比小于1.55，且120天线和200天线线呈上升趋势}
FYX63:=HHV(H,30)/LLV(L,120)<1.65 AND FYX603 AND FYX13;{30天内最高价与120日内最低价之比小于1.65，且长期均线呈多头排列，且满足FYX13}
FYX6:=FYX61 OR FYX62 OR FYX63;


FYX71:=HHV(H,5)/HHV(H,120)>0.85;{5天内最高价距离120日内的最高价不到15%}
FYX72:=HHV(H,5)/HHV(H,120)>0.8 AND FYX13;{5天内最高价距离120日内的最高价不到20%，且满足FYX13}
FYX73:=C/HHV(H,10)>0.9;{当天收盘价距离10日内的最高价不到10%}
FYX7:=(FYX71 OR FYX72) AND FYX73;


YXFZ:=FYX1 AND FYX2 AND FYX3 AND FYX4 AND FYX5 AND FYX6 AND FYX7; 
YXFZXG:=BARSSINCEN(YXFZ,15)=0;
DRAWICON(YXFZXG,LOW,34);{月线反转信号画图标34:黄色笑脸};
